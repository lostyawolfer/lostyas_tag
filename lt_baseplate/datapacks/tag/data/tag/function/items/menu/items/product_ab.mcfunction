$execute unless score @s menu.selecting matches 1.. unless score @s $(product).unlocked_$(id) matches 1 run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(page)}, custom_model_data=6, custom_name='[{"translate":"menu.$(product).$(id)","color":"yellow","italic":false}]']
$execute unless score @s menu.selecting matches 1.. if score @s $(product).unlocked_$(id) matches 1 unless score @s $(product).current matches $(id) run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(page)}, custom_model_data=5, custom_name='[{"translate":"menu.$(product).$(id)","color":"yellow","italic":false}]']
$execute unless score @s menu.selecting matches 1.. if score @s $(product).unlocked_$(id) matches 1 if score @s $(product).current matches $(id) run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(page)}, custom_model_data=27, custom_name='[{"translate":"menu.$(product).$(id)","color":"yellow","italic":false,"bold":true}]']

$execute if score @s menu.selecting matches 1.. unless score @s menu.selecting matches $(id) unless score @s $(product).unlocked_$(id) matches 1 run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(page)}, custom_model_data=8, custom_name='[{"translate":"menu.$(product).$(id)","color":"yellow","italic":false}]']
$execute if score @s menu.selecting matches 1.. unless score @s menu.selecting matches $(id) if score @s $(product).unlocked_$(id) matches 1 unless score @s $(product).current matches $(id) run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(page)}, custom_model_data=7, custom_name='[{"translate":"menu.$(product).$(id)","color":"yellow","italic":false}]']
$execute if score @s menu.selecting matches 1.. unless score @s menu.selecting matches $(id) if score @s $(product).unlocked_$(id) matches 1 if score @s $(product).current matches $(id) run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(page)}, custom_model_data=30, custom_name='[{"translate":"menu.$(product).$(id)","color":"yellow","italic":false,"bold":true}]']

$execute if score @s menu.selecting matches 1.. if score @s menu.selecting matches $(id) unless score @s $(product).unlocked_$(id) matches 1 run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(command)}, custom_model_data=10, custom_name='[{"translate":"menu.buy","color":"green","italic":false, "bold": true}, {"text": " \\""}, {"translate": "menu.$(product).$(id)"}, {"text": "\\"?"}, {"text": " ($(price) ", "italic": true, "bold": false}, {"translate": "menu.buy.price_$(price_count)", "italic": true, "bold": false}, {"text": ")", "italic": true, "bold": false}]']
$execute if score @s menu.selecting matches 1.. if score @s menu.selecting matches $(id) if score @s $(product).unlocked_$(id) matches 1 unless score @s $(product).current matches $(id) run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(command)}, custom_model_data=9, custom_name='[{"translate":"menu.select","color":"aqua","italic":false, "bold": true}, {"text": " \\""}, {"translate": "menu.$(product).$(id)"}, {"text": "\\"?"}]']
$execute if score @s menu.selecting matches 1.. if score @s menu.selecting matches $(id) if score @s $(product).unlocked_$(id) matches 1 if score @s $(product).current matches $(id) run item replace entity @s container.$(slot) with minecraft:warped_fungus_on_a_stick[custom_data={game:4, select:$(id), menu:$(command)}, custom_model_data=33, custom_name='[{"translate":"menu.$(product).deselect","color":"red","italic":false, "bold": true}]']